// Copyright Epic Games, Inc. All Rights Reserved.

#ifndef NUM_SAMPLES_PER_PIXEL_1D
	#define NUM_SAMPLES_PER_PIXEL_1D 1
	#define NUM_SAMPLES_PER_PIXEL_2D_X 1
	#define NUM_SAMPLES_PER_PIXEL_2D_Y 1
#endif

#include "../Common.ush"
#include "MegaLights.ush"

RWStructuredBuffer<uint> RWVisibleLightHash;
RWStructuredBuffer<uint> RWVisibleLightMaskHash;

StructuredBuffer<uint> VisibleLightHashBuffer;
StructuredBuffer<uint> VisibleLightMaskHashBuffer;

#ifdef FilterVisibleLightHashCS

uint2 VisibleLightHashViewMinInTiles;
uint2 VisibleLightHashViewSizeInTiles;

void AppendVisibleLightHash(uint2 BaseTileCoord, int2 TileOffset, inout uint VisibleLightHash[VISIBLE_LIGHT_HASH_SIZE], inout uint VisibleLightMaskHash[VISIBLE_LIGHT_HASH_SIZE])
{
	int2 TileCoord = BaseTileCoord + TileOffset;

	if (all(TileCoord >= (int2)VisibleLightHashViewMinInTiles) && all(TileCoord < (int2)(VisibleLightHashViewMinInTiles + VisibleLightHashViewSizeInTiles)))
	{
		uint LightHashBufferBase = VISIBLE_LIGHT_HASH_SIZE * (TileCoord.y * VisibleLightHashViewSizeInTiles.x + TileCoord.x);
		for (uint IndexInHash = 0; IndexInHash < VISIBLE_LIGHT_HASH_SIZE; ++IndexInHash)
		{
			VisibleLightHash[IndexInHash] |= VisibleLightHashBuffer[LightHashBufferBase + IndexInHash];
			VisibleLightMaskHash[IndexInHash] |= VisibleLightMaskHashBuffer[LightHashBufferBase + IndexInHash];
		}
	}
}

/**
 * Propagate visible light data by merging neighboring tiles.
 */
[numthreads(THREADGROUP_SIZE, THREADGROUP_SIZE, 1)]
void FilterVisibleLightHashCS(
	uint3 GroupId : SV_GroupID,
	uint3 GroupThreadId : SV_GroupThreadID,
	uint3 DispatchThreadId : SV_DispatchThreadID)
{
	uint2 TileCoord = DispatchThreadId.xy + VisibleLightHashViewMinInTiles;

	if (all(TileCoord >= VisibleLightHashViewMinInTiles) && all(TileCoord < VisibleLightHashViewMinInTiles + VisibleLightHashViewSizeInTiles))
	{
		uint VisibleLightHash[VISIBLE_LIGHT_HASH_SIZE];
		uint VisibleLightMaskHash[VISIBLE_LIGHT_HASH_SIZE];
		for (uint IndexInHash = 0; IndexInHash < VISIBLE_LIGHT_HASH_SIZE; ++IndexInHash)
		{
			VisibleLightHash[IndexInHash] = 0;
			VisibleLightMaskHash[IndexInHash] = 0;
		}

		AppendVisibleLightHash(TileCoord, int2(0, 0), VisibleLightHash, VisibleLightMaskHash);
		AppendVisibleLightHash(TileCoord, int2(+1, 0), VisibleLightHash, VisibleLightMaskHash);
		AppendVisibleLightHash(TileCoord, int2(-1, 0), VisibleLightHash, VisibleLightMaskHash);
		AppendVisibleLightHash(TileCoord, int2(0, +1), VisibleLightHash, VisibleLightMaskHash);
		AppendVisibleLightHash(TileCoord, int2(0, -1), VisibleLightHash, VisibleLightMaskHash);

		for (uint IndexInHash = 0; IndexInHash < VISIBLE_LIGHT_HASH_SIZE; ++IndexInHash)
		{
			uint LightHashBufferBase = VISIBLE_LIGHT_HASH_SIZE * (TileCoord.y * VisibleLightHashViewSizeInTiles.x + TileCoord.x);
			RWVisibleLightHash[LightHashBufferBase + IndexInHash] = VisibleLightHash[IndexInHash];
			RWVisibleLightMaskHash[LightHashBufferBase + IndexInHash] = VisibleLightMaskHash[IndexInHash];
		}
	}
}

#endif // FilterVisibleLightHashCS

#ifdef VolumeFilterVisibleLightHashCS

uint3 VolumeVisibleLightHashViewSizeInTiles;

void AppendVisibleLightHash(uint3 BaseVoxelCoord, int3 VoxelOffset, inout uint VisibleLightHash[VISIBLE_LIGHT_HASH_SIZE], inout uint VisibleLightMaskHash[VISIBLE_LIGHT_HASH_SIZE])
{
	int3 VoxelCoord = BaseVoxelCoord + VoxelOffset;

	if (all(VoxelCoord >= 0) && all(VoxelCoord < int3(VolumeVisibleLightHashViewSizeInTiles)))
	{
		uint LightHashBufferBase = VISIBLE_LIGHT_HASH_SIZE * ((VoxelCoord.z * VolumeVisibleLightHashViewSizeInTiles.y + VoxelCoord.y) * VolumeVisibleLightHashViewSizeInTiles.x + VoxelCoord.x);
		for (uint IndexInHash = 0; IndexInHash < VISIBLE_LIGHT_HASH_SIZE; ++IndexInHash)
		{
			VisibleLightHash[IndexInHash] |= VisibleLightHashBuffer[LightHashBufferBase + IndexInHash];
		}
	}
}

/**
 * Propagate visible light data by merging neighboring voxels.
 */
[numthreads(THREADGROUP_SIZE, THREADGROUP_SIZE, THREADGROUP_SIZE)]
void VolumeFilterVisibleLightHashCS(
	uint3 GroupId : SV_GroupID,
	uint3 GroupThreadId : SV_GroupThreadID,
	uint3 DispatchThreadId : SV_DispatchThreadID)
{
	uint3 VoxelCoord = DispatchThreadId.xyz;

	if (all(VoxelCoord < VolumeVisibleLightHashViewSizeInTiles))
	{
		uint VisibleLightHash[VISIBLE_LIGHT_HASH_SIZE];
		uint VisibleLightMaskHash[VISIBLE_LIGHT_HASH_SIZE];
		for (uint IndexInHash = 0; IndexInHash < VISIBLE_LIGHT_HASH_SIZE; ++IndexInHash)
		{
			VisibleLightHash[IndexInHash] = 0;
			VisibleLightMaskHash[IndexInHash] = 0;
		}

		AppendVisibleLightHash(VoxelCoord, int3(0, 0, 0), VisibleLightHash, VisibleLightMaskHash);
		AppendVisibleLightHash(VoxelCoord, int3(+1, 0, 0), VisibleLightHash, VisibleLightMaskHash);
		AppendVisibleLightHash(VoxelCoord, int3(-1, 0, 0), VisibleLightHash, VisibleLightMaskHash);
		AppendVisibleLightHash(VoxelCoord, int3(0, +1, 0), VisibleLightHash, VisibleLightMaskHash);
		AppendVisibleLightHash(VoxelCoord, int3(0, -1, 0), VisibleLightHash, VisibleLightMaskHash);
		AppendVisibleLightHash(VoxelCoord, int3(0, 0, +1), VisibleLightHash, VisibleLightMaskHash);
		AppendVisibleLightHash(VoxelCoord, int3(0, 0, -1), VisibleLightHash, VisibleLightMaskHash);

		for (uint IndexInHash = 0; IndexInHash < VISIBLE_LIGHT_HASH_SIZE; ++IndexInHash)
		{
			uint LightHashBufferBase = VISIBLE_LIGHT_HASH_SIZE * ((VoxelCoord.z * VolumeVisibleLightHashViewSizeInTiles.y + VoxelCoord.y) * VolumeVisibleLightHashViewSizeInTiles.x + VoxelCoord.x);
			RWVisibleLightHash[LightHashBufferBase + IndexInHash] = VisibleLightHash[IndexInHash];
		}
	}
}

#endif // VolumeFilterVisibleLightHashCS